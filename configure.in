# $Id: configure.in,v 1.34.2.8 2008/10/16 18:08:16 wiede Exp $

AC_REVISION($Revision: 1.34.2.8 $)
AC_INIT([Itcl],[4.0])
AC_CONFIG_AUX_DIR(tclconfig)

TEA_INIT([3.6])
TEA_PATH_TCLCONFIG
TEA_LOAD_TCLCONFIG
TEA_PATH_TKCONFIG
TEA_LOAD_TKCONFIG
TEA_PREFIX
TEA_SETUP_COMPILER
TEA_ADD_SOURCES(
	itclBase.c itclHelpers.c itcl2TclOO.c
	itclUtil.c itclLinkage.c itclTestRegisterC.c
	itclParse.c itclClass.c itclObject.c itclMethod.c
	itclStubs.c itclStubInit.c
	itclResolve.c itclMigrate2TclCore.c
	itclTclIntStubsFcn.c
	itclResolve2.c itclVarsAndCmds.c
	itclBuiltin.c itclEnsemble.c itclCmd.c
	itclInfo.c itclNeededFromTclOO.c)
AS_IF([test ${TEA_PLATFORM} = "windows"], [
    PKG_OBJECTS="${PKG_OBJECTS} ${PACKAGE_NAME}.res.o"
    RC=windres
    AC_SUBST(RC)])
TEA_ADD_STUB_SOURCES(itclStubLib.c)
TEA_ADD_HEADERS([generic/itcl.h generic/itclInt.h])
TEA_ADD_INCLUDES([-I. -I\"`${CYGPATH} ${srcdir}/generic`\"])
TEA_PRIVATE_TCL_HEADERS
TEA_ENABLE_THREADS
TEA_ENABLE_SHARED
TEA_ENABLE_SYMBOLS
TEA_CONFIG_CFLAGS

CLEANFILES="$CLEANFILES pkgIndex.tcl"
AC_SUBST(CLEANFILES)
AC_DEFINE(USE_TCL_STUBS,[1],[Should always be 1])
AC_DEFINE(BUILD_itcl,[1],[Set to 1 when building package])

TEA_MAKE_LIB

TEA_PROG_TCLSH
CONFIGURE_OUTPUTS="Makefile config.cache config.log config.status itclConfig.sh"
AC_SUBST(CONFIGURE_OUTPUTS)

AC_PATH_PROG(SDX, sdx, none)
AS_IF([test "${SDX}" = "none"],[
    AC_PATH_PROG(SDX_KIT, sdx.kit, none)
    AS_IF([test "${SDX_KIT}" != "none"],[
	# We assume that sdx.kit is on the path, and that the default tclsh
	# is activetcl
	SDX="tclsh '${SDX_KIT}'"])])
AS_IF([test "${SDX}" = "none"],[
    AC_MSG_WARN([cannot find sdx; building starkits will fail; building as a normal library still supported])])

# TODO: Adapt this for OSX Frameworks...
# This next bit is a bit ugly, but it makes things for itclConfig.sh...
eval "srcdir2=\"`pwd`\""
eval "ITCL_SRC_DIR=\"`${CYGPATH} ${srcdir2}`\""
eval "ITCL_PRIVATE_INCLUDE_DIR=\"`${CYGPATH} ${srcdir2}/generic`\""
case ${ITCL_PRIVATE_INCLUDE_DIR} in
    /*) ITCL_PRIVATE_INCLUDE_SPEC="-I${ITCL_PRIVATE_INCLUDE_DIR}" ;;
        *)  ITCL_PRIVATE_INCLUDE_SPEC="-I`pwd`/${ITCL_PRIVATE_INCLUDE_DIR}" ;;
	esac
eval "TCL_INCLUDE_SPEC=\"-I`${CYGPATH} ${includedir}`\""
if test ${TCL_LIB_VERSIONS_OK} = nodots; then
    eval "ITCL_LIB_SPEC=\"-L`${CYGPATH} ${libdir}/${PACKAGE_NAME}${PACKAGE_VERSION}` -l${PACKAGE_NAME}${TCL_TRIM_DOTS}\""
    eval "ITCL_STUB_LIB_SPEC=\"-L`${CYGPATH} ${libdir}/${PACKAGE_NAME}${PACKAGE_VERSION}` -l${PACKAGE_NAME}stub${TCL_TRIM_DOTS}\""
else
    eval "ITCL_LIB_SPEC=\"-L`${CYGPATH} ${libdir}/${PACKAGE_NAME}${PACKAGE_VERSION}` -l${PACKAGE_NAME}${PACKAGE_VERSION}\""
    eval "ITCL_STUB_LIB_SPEC=\"-L`${CYGPATH} ${libdir}/${PACKAGE_NAME}${PACKAGE_VERSION}` -l${PACKAGE_NAME}stub${PACKAGE_VERSION}\""
fi
AC_SUBST(ITCL_INCLUDE_SPEC)
AC_SUBST(ITCL_PRIVATE_INCLUDE_SPEC)
AC_SUBST(ITCL_LIB_SPEC)
AC_SUBST(ITCL_STUB_LIB_SPEC)
AC_SUBST(ITCL_SRC_DIR)


AC_CONFIG_FILES([Makefile itclConfig.sh])
AC_OUTPUT
